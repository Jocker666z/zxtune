buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "app.zxtune"
        minSdkVersion 14
        targetSdkVersion 27
        //supported languages
        resConfigs "en", "de", "ru", "es", "cs"
        //supported densities for vector drawables rasterization
        generatedDensities = ["mdpi", "hdpi", "xhdpi", "xxhdpi"]
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:27.1.1"
    implementation "com.android.support:support-media-compat:27.1.1"
    implementation "com.android.support:support-annotations:27.1.1"
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:crashlytics-ndk:2.0.1@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }
    implementation "org.jsoup:jsoup:1.11.3"
}

apply from: "${treeRoot}/make/android/android.gradle"
apply from: "${treeRoot}/make/android/nativelibs.gradle"

android {
    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

android {
    defaultConfig {
        manifestPlaceholders = [ modarchiveKey: localProperties.getProperty("key.modarchive"),
                                 crashlyticsKey: localProperties.getProperty("key.crashlytics") ]

        def cdnRoot = localProperties.getProperty('cdn.root')
        if (cdnRoot != null) {
            buildConfigField("String", "CDN_ROOT", "\"${cdnRoot}\"")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//http://stackoverflow.com/questions/40134153/crashlytics-ndk-symbol-building-from-command-line
//http://stackoverflow.com/questions/33735332/crashlytics-ndk-multi-androidndkout-path-support
android {
    ext.crashlyticsTmpDir = "${project.buildDir}/crashlytics"
    ext.crashlyticsNdkOut = "${crashlyticsTmpDir}/debug"
    ext.crashlyticsNdkLibsOut = "${crashlyticsTmpDir}/release"

    //crashlytics tasks should be performed in the same execution due to apk rebuild
    task publicBuildWithCrashlytics(dependsOn: publishPdb)
    
    applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def taskName = "prepareDebugSymbols${variantName}"
        task(taskName, dependsOn: ["nativeLibsDebugSymbols${variantName}", "assemble${variantName}"]) {
            def libsDir = variant.ndkCompile.soFolder
            def objsDir = variant.ndkCompile.objFolder
            doFirst {
                fileTree(dir: objsDir, include: '**/*.pdb').each { file ->
                    def target = file.getAbsolutePath().replace('so.pdb', 'so')
                    file.renameTo(target)
                }
            }
            doLast {
                new File(crashlyticsTmpDir).deleteDir()
                copy {
                    from objsDir
                    into crashlyticsNdkOut
                }
                copy {
                    from libsDir
                    into crashlyticsNdkLibsOut
                }
            }
        }
        
        tasks.getByName("crashlyticsGenerateSymbols${variantName}").dependsOn.add(taskName)
        if (variant.buildType.name == "release") {
            publicBuildWithCrashlytics.dependsOn.addAll(["crashlyticsUploadSymbols${variantName}", "publishApk${variantName}"])
        }
    }
}

crashlytics {
    enableNdk true
    androidNdkOut android.crashlyticsNdkOut
    androidNdkLibsOut android.crashlyticsNdkLibsOut
}
